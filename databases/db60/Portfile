# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem      1.0
PortGroup       muniversal 1.0

name            db60
version         6.0.30
revision        3
set branch      [join [lrange [split ${version} .] 0 1] {}]
categories      databases
license         agpl-3
maintainers     nomaintainer
description     The Berkeley DB package, version 6.0
long_description \
    Version 6.0 of the Berkeley Data Base library which \
    offers (key/value) storage with optional concurrent \
    access or transactions interface.  This port will \
    install the AES (American Encryption Standard) \
    enabled version.

homepage        http://www.oracle.com/us/products/database/berkeley-db/db/overview/index.html
master_sites    http://download.oracle.com/berkeley-db/

distname        db-${version}

checksums       rmd160  8684dba9e28ddc14dede4b633f2afb56f79c3b89 \
                sha256  608e4b1cf390e9bf54c0ef00c5bd9ca76d36e2261b9f4d33d54516f3f6a20fd2 \
                size    36584356

patchfiles      yosemite-libtool.patch \
                patch-src_dbinc_atomic.h \
                patch-fix-macro-sqlite.diff

configure.dir   ${worksrcpath}/build_unix
build.dir       ${configure.dir}

configure.cmd   ../dist/configure
configure.args  --enable-cxx \
                --includedir=\\\${prefix}/include/db${branch} \
                --libdir=\\\${prefix}/lib/db${branch}

set cxx_stdlibflags {}
if {[string match *clang* ${configure.cxx}] && ${configure.cxx_stdlib} ne ""} {
    set cxx_stdlibflags -stdlib=${configure.cxx_stdlib}
}
configure.cxx ${configure.cxx} ${cxx_stdlibflags}

destroot.destdir \
    prefix=${destroot}${prefix} \
    docdir=${destroot}${prefix}/share/doc/${name}

post-destroot {
    foreach bin [glob -tails -directory ${destroot}${prefix}/bin/ db_*] {
        set newbin [regsub {^db_} ${bin} "db${branch}_"]
        move ${destroot}${prefix}/bin/${bin} \
            ${destroot}${prefix}/bin/${newbin}
    }
    if { [variant_isset sql] } {
        move ${destroot}${prefix}/bin/dbsql \
            ${destroot}${prefix}/bin/db${branch}sql
    }
}

# We cannot use jdk8 here, since the source explicitly prohibits < 1.7 and fails:
# Exception in thread "main" java.lang.IllegalArgumentException
set jdk_v                   7
set java_home               /Library/Java/JavaVirtualMachines/openjdk${jdk_v}-bootstrap

variant java description {Build the Java API} {
    # error: expected '=', ',', ';', 'asm' or '__attribute__' before 'JNICALL'
    compiler.blacklist-append \
                            *gcc-4.0 *gcc-4.2

    depends_build-append    port:openjdk${jdk_v}-bootstrap

    configure.env-append    JAVA_HOME=${java_home}
    configure.cppflags-append \
                            -I${java_home}/include
    platform darwin {
        # jni.h:45:10: fatal error: jni_md.h: No such file or directory
        configure.cppflags-append \
                            -I${java_home}/include/darwin
    }
    configure.args-append   --enable-java \
                            ac_cv_prog_JAR=${java_home}/bin/jar \
                            ac_cv_prog_JAVA=${java_home}/bin/java \
                            ac_cv_prog_JAVAC=${java_home}/bin/javac
}

if {${os.subplatform} eq "macosx" && ${os.major} < 11} {
    default_variants +java
}

variant sql description {Build the SQL API} {
    configure.args-append   --enable-sql
# build with -DSQLITE_ENABLE_COLUMN_METADATA
# required by libgda5 to enable Berkeley DB SQL support
    configure.cppflags-append -DSQLITE_ENABLE_COLUMN_METADATA
}
default_variants +sql

variant tcl description {build Tcl API} {
    depends_lib-append      port:tcl
    configure.args-append   --enable-tcl --with-tcl=${prefix}/lib
}

if { [variant_isset universal] } {
    # configure fails with aarch64 but it seems to work fine with arm
    set merger_host(arm64) arm-apple-${os.platform}${os.version}

    if {${os.arch} eq "i386"} {
        if { ${os.major} >= 10 } {
            set merger_configure_args(ppc) --with-mutex=Darwin/_spin_lock_try
        }
        set merger_configure_args(ppc64)   --with-mutex=Darwin/_spin_lock_try
    } else {
        set merger_configure_args(i386)    --with-mutex=x86/gcc-assembly
        set merger_configure_args(x86_64)  --with-mutex=x86_64/gcc-assembly
    }
}

livecheck.type  none
